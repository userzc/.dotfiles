# This file is a rip-off from the edit-command-line on recent zsh
# versions, it is included here to have a working edit-command-line but
# is should be unnecessary for zsh versions >= 5.2
# # Original zsh 5.2 (x86_64-ubuntu-linux-gnu)
() {
  exec </dev/tty
  # Compute the cursor's position in bytes, not characters.
  setopt localoptions nomultibyte
  # Open the editor, placing the cursor at the right place if we know how.
  local editor=${${VISUAL:-${EDITOR:-vi}}}
  case $editor in
    (*vim*)
      integer byteoffset=$(( $#PREBUFFER + $#LBUFFER + 1 ))
      ${=editor} -c "normal! ${byteoffset}go" -- $1;;
    (*emacs*)
      local lines=( ${(f):-"$PREBUFFER$LBUFFER"} )
      ${=editor} +${#lines}:$((${#lines[-1]} + 1)) $1;;
    (*) ${=editor} $1;;
  esac
  # Replace the buffer with the editor output.
  print -Rz - "$(<$1)"
} =(<<<"$PREBUFFER$BUFFER")
zle send-break		# Force reload from the buffer stack
